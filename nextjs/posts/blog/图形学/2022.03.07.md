---
title: 用OpenGL在GPU上实现路径追踪——开始追踪
date: 2022-03-07
---

## 1. 光线定义与求交

光线有许多表示方法，这里我们使用起点与方向表示，同样还有光线的碰撞结果，包含了撞击点的一些信息，最终它们的结构体如下：
```cpp
struct Ray {
    vec3 startPoint;
    vec3 direction;
};
struct HitResult {
    bool isHit;
    bool isInside;
    float distance;
    vec3 hitPoint;
    vec3 normal;
    vec2 texCoords;
    vec3 viewDir;
    mat3 TBN;
    Material material;
};
```
然后就是光线与三角面求交了，首先要讲一下三角形的重心坐标，简单来说就是一个三角面中的任意一点都可以表示为三个顶点的线性组合，这三个系数和为1且都在$[0, 1]$内，即
$$
\bold{p} = (1-u-v)\bold{p_0} + u\bold{p_1} + v\bold{p_2}
$$
其中$\bold{p}$为三角形内点坐标，$\bold{p_i}$为三个顶点，同样这个点还可以用光线表示，我们令光线起点为$\bold{o}$，光线方向为$\bold{d}$，$t$为光线起点到交点距离，即
$$
\bold{p} = \bold{o}+t\bold{d}
$$
因此有
$$
\bold{o}+t\bold{d} = (1-u-v)\bold{p_0} + u\bold{p_1} + v\bold{p_2}
$$
我们要求的变量为$u, v, t$，因此将方程表示为矩阵：
$$
\left(\begin{array}{l}
-\bold{d} & \bold{p_1}-\bold{p_0} & \bold{p_2}-\bold{p_0}
\end{array}\right)
\left(\begin{array}{l}
t \\
u \\
v
\end{array}\right)
= \bold{o} - \bold{p_0}
$$
令$\bold{e_1}=\bold{p_1}-\bold{p_0}, \bold{e_2} = \bold{p_1}-\bold{p_0}, \bold{s} = \bold{o} - \bold{p_0}$，由克拉默法则：
$$
\left(\begin{array}{l}
t \\
u \\
v
\end{array}\right)
= \frac{1}{|-\bold{d}, \bold{e_1}, \bold{e_2}|}
\left(\begin{array}{l}
|\bold{s}, \bold{e_1}, \bold{e_2}| \\
|-\bold{d}, \bold{s}, \bold{e_2}| \\
|-\bold{d}, \bold{e_1}, \bold{s}|
\end{array}\right)
$$
又由$|a,b,c|=(c\times a)\cdot b=(a\times b)\cdot c=(b\times c)\cdot a$
$$
\left(\begin{array}{l}
t \\
u \\
v
\end{array}\right)
= \frac{1}{(\bold{d}\times \bold{e_2})\cdot \bold{e_1}}
\left(\begin{array}{l}
(\bold{s}\times \bold{e_1})\cdot \bold{e_2} \\
(\bold{d}\times \bold{e_2})\cdot \bold{s} \\
(\bold{s}\times \bold{e_1})\cdot \bold{d}
\end{array}\right)
$$
因此$t, u, v$都能简单地算出来了：
```cpp
HitResult hitTriangle(Triangle triangle, Ray ray)
{
    HitResult res;
    res.distance = 0;
    res.isHit = false;

    vec3 O = ray.startPoint;
    vec3 D = ray.direction;
    vec3 E1 = triangle.p[1] - triangle.p[0];
    vec3 E2 = triangle.p[2] - triangle.p[0];
    vec3 S1 = cross(D, E2);

    float SE = dot(S1, E1);
    // parallel
    if (abs(SE) < 0.000001f) return res;

    float invdet = 1 / SE;
    vec3 S = O - triangle.p[0];

    float u = dot(S1, S) * invdet;
    // out of triangle
    if (u < 0 || u > 1) return res;

    vec3 S2 = cross(S, E1);
    float v = dot(S2, D) / SE;
    // out of triangle
    if (v < 0 || u + v > 1) return res;

    float t = dot(S2, E2) / SE;
    if (t < 0.0005f) return res;

    res.isHit = true;
    ...
}
```
通过SE大小判断光线是否与三角面平行，通过$u, v$是否越界判断交点在三角形外，通过$t$的大小判断光线起点是否在三角形面内。这之后就是获取碰撞结果并返回了。

## 2. 半球面采样

光线在弹射时需要生成一条随机方向的光线，因此我们需要实现一个随机函数。首先通过当前像素坐标和帧数生成一个随机种子，使得不同像素不同时间得到的随机数不同，然后使用Jenkins hash算法映射，归一化后得到随机数：
```cpp
uint seed = uint(
    uint(gl_FragCoord.x) * uint(1973) + 
    uint(gl_FragCoord.y) * uint(9277) + 
    uint(frameCounter) * uint(26699)) | uint(1);
uint hash(uint seed)
{
    seed = uint(seed ^ uint(61)) ^ uint(seed >> uint(16));
    seed *= uint(9);
    seed = seed ^ (seed >> 4);
    seed *= uint(0x27d4eb2d);
    seed = seed ^ (seed >> 15);
    return seed;
}
float rand()
{
    return float(hash(seed)) / 4294967296.0;
}
```
有了随机函数，半球面采样的实现就很简单了：
```cpp
vec3 SampleHemisphere() {
    float z = rand();
    float r = max(0, sqrt(1.0 - z*z));
    float phi = 2.0 * PI * rand();
    return vec3(r * cos(phi), r * sin(phi), z);
}
```
这里得到的方向是在切线空间中的，乘以TBN矩阵转换到世界空间：
```cpp
vec3 L = TBN * SampleHemisphere();
```

## 3. 追踪

有了这些函数，接下来就可以追踪了，首先回顾一下渲染方程：
$$
L_o(p, \omega_o) = L_e(p, \omega_o) + \int_{\Omega^+}f_r(p, \omega_i, \omega_o)L_i(p, \omega_i)cos\theta d\omega_i
$$
由光路可逆，将$L_i$看作从p点射出的光线，渲染方程就是个递归式，但shader中不能递归，所以只能将递归展开，最后就像这样
```cpp
vec3 tracing(HitResult hit, int maxBounce)
{
    vec3 Lo = vec3(0);
    vec3 nowL = vec3(1);

    for (int bounce = 0; bounce < maxBounce; bounce ++) {
        vec3 V = -hit.viewDir;
        vec3 N = hit.normal;
        vec3 L = hit.TBN * SampleHemisphere();
        
        Ray randomRay;
        randomRay.startPoint = hit.hitPoint;
        randomRay.direction = L;

        HitResult newHit;
        newHit.isHit = false;
        for (int i = 0; i <= nTriangles; i ++) {
            Triangle triangle = getTriangle(i);
            HitResult newhit = hitTriangle(triangle, randomRay);
            if (newhit.isHit && (!newHit.isHit || newhit.distance < newHit.distance)) {
                Material mat = getMaterial(i, newhit.texCoords);
                newHit = newhit;
                newHit.material = mat;
            }
        }

        if (!newHit.isHit) {
            break;
        }
        
        vec3 f_r = hit.material.diffuse;
        float pdf = 1.0 / 2 * PI;
        float NdotL = dot(N, L);

        vec3 Le = newHit.material.emissive;
        Lo += nowL * Le * f_r * NdotL / pdf;             

        hit = newHit;
        nowL *= f_r * NdotL / pdf;
    }
    
    return Lo;
}
```
这里fr只是物体的漫反射，求交也是暴力求交，之后会进行优化。